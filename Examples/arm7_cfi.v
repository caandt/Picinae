Require Export Picinae_armv7_lifter.
Require Import NArith.
Require Import ZArith.
Require Import Bool.
Require Import Coq.Lists.List.
From Coq Require Recdef.
Require Import Lia.
Import ListNotations.
Open Scope Z.
Require Extraction.
Extraction Language OCaml.
Set Extraction Output Directory "arm_cfi_extraction".

Definition Z_4 := -4.
Definition Z_8 := -8.
Definition Z_32 := -32.
Definition Z0xff := 0xff.
Definition Z0xffff := 0xffff.
Definition Z32767 := 32767.
Definition Z1245169 := 1245169.
Definition Z1245171 := 1245171.
Definition Z33554428 := 33554428.
Definition Z_33554432 := -33554432.
Definition Z4294967296 := 4294967296.
Definition Z_8388608 := -8388608.
Definition Z8388607 := 8388607.

Definition Z_popcount z :=
  match z with Z0 => Z0
  | Z.pos p => Z.pos (Pos_popcount p)
  | _ => Z0
  end.

(* H(x) = (x << sl) >> sr *)
Definition apply_hash sl sr z :=
  Z.shiftr (Z.land (Z.shiftl z sl) (Z.ones Z32)) sr.

(* checks if l contains z *)
Definition contains z l :=
  match find (Z.eqb z) l with
  | Some _ => true
  | None => false
  end.
(* horrible jank to extract this to ocaml that uses a hash table instead *)
Definition tbl_contains := contains.
Extract Constant tbl_contains => "(fun a b -> Hashtbl.mem b a)".
Definition tbl_add (tbl:list Z) (hi hi':Z) := hi::hi'::tbl.
Extract Constant tbl_add => "(fun tbl a b -> (Hashtbl.add tbl a (); if a <> b then Hashtbl.add tbl b () else (); tbl))".
(* checks if the hash produces no unacceptable collisions
   dis dis' - list of old/new destination indexes
   sl sr - hash parameters *)
Fixpoint validhash tbl dis dis' sl sr :=
  match dis, dis' with
  | a::t, a'::t' =>
      let ha := apply_hash sl sr a in
      let ha' := apply_hash sl sr a' in
      if tbl_contains ha tbl || tbl_contains ha' tbl then false
      else validhash (tbl_add tbl ha ha') t t' sl sr
  | _, _ => true
  end.
Definition w_nil (f:list Z -> list Z -> list Z -> Z -> Z -> bool) := f nil.
Extract Inlined Constant w_nil => "(fun f dis -> let seen = Hashtbl.create (List.length dis) in f seen dis)".

Function find_sr dis dis' sl sr {measure Z.to_nat sr} :=
  if sr <=? Z0 then None
  else if w_nil validhash dis dis' sl sr then Some sr else find_sr dis dis' sl (sr-Z1).
Proof. unfold Z1 in *. lia. Qed.

Function find_hash dis dis' sl {measure Z.to_nat sl} :=
  if sl <=? Z2 then None
  else match find_sr dis dis' sl Z31 with
       | Some sr => Some (sl, sr)
       | None => find_hash dis dis' (sl-Z1)
       end.
Proof. unfold Z1, Z2 in *. lia. Qed.

Definition map_add (f:Z->Z) j k d := fun x => if (x =? j) || (x =? k) then d else f x.
Extract Inlined Constant map_add => "(fun tbl j k d -> Hashtbl.add tbl j d; Hashtbl.add tbl k d; tbl)".

(* make a function that maps table index to the value in the table at that index
   dis dis' - list of old/new destination indexes
   sl, sr - from find_hash
   f - default value (fun _ -> abort address) *)
Fixpoint make_jump_table_map dis dis' sl sr f :=
  match dis, dis' with
  | di::t, di'::t' =>
      let j := apply_hash sl sr di in
      let k := apply_hash sl sr di' in
      let f' := make_jump_table_map t t' sl sr f in
      map_add f' j k (di'*Z4)
  | _, _ => f
  end.

Function map2list (m: Z -> Z) n
    {measure Z.to_nat n} :=
  if n >? Z0 then m (n-Z1)::map2list m (n-Z1)
  else nil.
Proof. intros. unfold Z1 in *. lia. Qed.

Fixpoint _map2list (m: Z -> Z) n :=
  match n with
  | S n' => m (Z.of_nat n')::_map2list m n'
  | O => nil
  end.
Lemma map2list_func : forall m n, _map2list m n = map2list m (Z.of_nat n).
Proof.
  intros. induction n.
    now rewrite map2list_equation.
    simpl. rewrite IHn, map2list_equation with (n := Z.pos _).
      now replace (_ - _) with (Z.of_nat n) by (unfold Z1; lia).
Qed.
Lemma map2list_fix : forall m n, map2list m n = _map2list m (Z.to_nat n).
Proof.
  intros. symmetry. destruct n.
    rewrite <- Nat2Z.inj_0. apply map2list_func.
    rewrite <- (Z2Nat.id _ (Zle_0_pos _)) at 2. apply map2list_func.
    now rewrite map2list_equation.
Qed.

Definition make_jump_table (mjtm:list Z -> list Z -> Z -> Z -> (Z -> Z) -> Z -> Z) dis dis' ai sl sr n :=
  let m := mjtm dis dis' sl sr (fun _ => ai * Z4) in
  rev (map2list m n).
Extract Inlined Constant make_jump_table => "(fun mjtm dis dis' ai sl sr n ->
  let m = mjtm dis dis' sl sr (Hashtbl.create (2*n)) in
  List.init n (fun x -> match Hashtbl.find_opt m x with | Some y -> y | _ -> ai * 4)
)".

Definition PC := Z15.
Definition LR := Z14.
Definition SP := Z13.
Definition ALIGN rt :=
  ARM_data_i ARM_BIC Z14 Z0 rt rt Z3.
Definition STR rt rn offset :=
  let U := if offset <? Z0 then Z0 else Z1 in
  ARM_ls_i ARM_STR Z14 Z1 U Z0 rn rt (Z.abs offset).
Definition LDR rt rn offset :=
  let U := if offset <? Z0 then Z0 else Z1 in
  ARM_ls_i ARM_LDR Z14 Z1 U Z0 rn rt (Z.abs offset).
Definition MOVW rd imm :=
  ARM_MOV_WT true Z14 ((imm >> Z12) & Z15) rd (imm & Z4095).
Definition MOVT rd imm :=
  ARM_MOV_WT false Z14 ((imm >> Z12) & Z15) rd (imm & Z4095).
Definition MOV rd rm :=
  ARM_data_r ARM_MOV Z14 Z0 Z0 rd 0 Z0 rm.
Definition LSL rd rm imm :=
  ARM_data_r ARM_MOV Z14 Z0 Z0 rd imm Z0 rm.
Definition LSR rd rm imm :=
  ARM_data_r ARM_MOV Z14 Z0 Z0 rd imm Z1 rm.
Definition STMDB2 rn r0 r1 :=
  ARM_lsm ARM_STMDB Z14 Z0 rn ((Z1 << r0) .| (Z1 << r1)).
Definition LDMDB2 rn r0 r1 :=
  ARM_lsm ARM_LDMDB Z14 Z0 rn ((Z1 << r0) .| (Z1 << r1)).
Definition STMDB3 rn r0 r1 r2 :=
  ARM_lsm ARM_STMDB Z14 Z0 rn ((Z1 << r0) .| (Z1 << r1) .| (Z1 << r2)).
Definition LDMDB3 rn r0 r1 r2 :=
  ARM_lsm ARM_LDMDB Z14 Z0 rn ((Z1 << r0) .| (Z1 << r1) .| (Z1 << r2)).
Definition UBFX rd rn sl sr :=
  ARM_bfx false Z14 (Z31-sr) rd (sr-sl) rn.
Definition GOTO (l: bool) (cond src dest: Z) :=
  let offset := dest - src - Z2 in
  if (offset <? Z_8388608) || (offset >? Z8388607) then None
  else
    let imm := Z.land offset (Z.ones Z24) in
    Some ((if l then ARM_BL else ARM_B) cond imm).
Definition Z0xe1200070 := 0xe1200070.
Extract Inlined Constant Z0xe1200070 => "0xe1200070".
Definition GOTOz l cond src dest :=
  match GOTO l cond src dest with
  | Some a => arm_assemble a
  | None => Some (Z0xe1200070)
  end.


Definition arm_add (reg imm: Z) : list arm_inst :=
  let a := ARM_data_i ARM_ADD Z14 Z0 reg reg in
    a ((Z4 << Z8) .| (zxbits imm Z24 Z32))::
    a ((Z8 << Z8) .| (zxbits imm Z16 Z24))::
    a ((Z12 << Z8) .| (zxbits imm Z8 Z16))::
    a (zxbits imm Z0 Z8)::nil.
(* reg = table[H(reg)] *)
Definition arm_table_lookup ti sl sr reg :=
  [ UBFX reg reg (sl-Z2) sr;
    LSL reg reg Z2          (* lsl reg, reg, #2 *)
  ]++arm_add reg (Z4*ti)++[ (* add reg, reg, #4*ti *)
    LDR reg reg Z0          (* ldr reg, [reg] *)
  ].
Definition arm_table_lookup2 ti sl sr reg reg2 :=
  [ UBFX reg reg (sl-Z2) sr;
    LSL reg reg Z2          (* lsl reg, reg, #2 *)
  ]++arm_add reg (Z4*ti)++[ (* add reg, reg, #4*ti *)
    LDR reg reg Z0          (* ldr reg, [reg] *)
  ].

(*
   cond - condition number of instruction to rewrite
   i - old index of instruction to rewrite
   ti - index of table to use
   sl - shift left hash parameter
   sr - shift right hash parameter

   returns the list of new instruction encodings
 *)
Definition IRM := Z -> Z -> Z -> Z -> Z -> option (list Z).
(* list of destination indices -> option (ti, sl, sr) *)
Definition TableCache := list Z -> option (Z * Z * Z).
(* option (list of new instruction encodings, list of table entries, updated table cache) *)
Definition NewInst := option (list Z * list Z * TableCache).

Definition wo_table z' tc : NewInst :=
  match z' with
  | Some z' => Some (z', nil, tc)
  | None => None
  end.

Definition invert_cond cond :=
  if (cond mod Z2 =? Z0) then cond + Z1
  else cond - Z1.

(* assemble insts and add a branch to the start if conditional *)
Definition arm_assemble_all_cond insts cond :=
  let jump_after := ARM_B (invert_cond cond) (Z.of_nat (length insts) - Z1) in
  if (cond <? Z14) then arm_assemble_all (jump_after::insts)
  else arm_assemble_all insts.

Fixpoint list_eqb l1 l2 :=
  match l1, l2 with
  | a::b, c::d => if a =? c then list_eqb b d else false
  | nil, nil => true
  | _, _ => false
  end.
Definition rewrite_w_table
    (irm: IRM)
    (tc: TableCache)
    (dis: list Z)
    (i2i': Z -> Z)
    (cond i ti ai: Z)
    : NewInst :=
  match tc dis with
  | None =>
      let dis' := map i2i' dis in
      match find_hash dis dis' Z31 with
      | None => None
      | Some (sl, sr) =>
          match irm cond i ti sl sr with
          | None => None
          | Some irm =>
              let table := make_jump_table make_jump_table_map dis dis' ai sl sr (Z1 << (Z32 - sr)) in
              let tc' := fun x => if (list_eqb x dis) then Some (ti, sl, sr) else tc x in
              if (ti + Z.of_nat (length table) >=? (Z1 << Z30)) then None else
              Some (irm, table, tc')
          end
      end
  | Some (ti, sl, sr) =>
      match irm cond i ti sl sr with
      | None => None
      | Some irm => Some (irm, nil, tc)
      end
  end.

Definition bx_blx_irm (l: bool) reg : IRM :=
  fun cond i ti sl sr =>
    arm_assemble_all_cond (
      arm_table_lookup ti sl sr reg++  (* reg = table[H(reg)] *)
      (if l then ARM_BLX_r else ARM_BX) Z14 reg::nil              (* bx reg *)
    ) cond.
Definition rewrite_bx_blx l reg := rewrite_w_table (bx_blx_irm l reg).
Definition rewrite_bx reg := rewrite_bx_blx false reg.
Definition rewrite_blx reg := rewrite_bx_blx true reg.
Definition ldm_pc_irm op Rn register_list reg orig_inst : IRM :=
  fun cond i ti sl sr =>
    let bc := Z4 * Z_popcount (Z.land register_list (Z.ones Z16)) in
    let offset := arm_lsm_op_start op bc + bc - Z4 in
    arm_assemble_all_cond ([
      STR reg SP Z_4;                     (* str reg, [sp, #-4] *)
      LDR reg Rn offset                   (* ldr reg, [Rn, #pc_offset] *)
    ]++arm_table_lookup ti sl sr reg++[   (* reg = table[H(reg)] *)
      STR reg Rn offset;                  (* str reg, [Rn, #pc_offset] *)
      LDR reg SP Z_4;                     (* ldr reg, [sp, #-4] *)
      orig_inst                           (* original inst *)
    ]) cond.
Definition rewrite_ldm_pc op Rn register_list reg orig_inst := rewrite_w_table (ldm_pc_irm op Rn register_list reg orig_inst).

(* irm for instructions that use pc as a destination register, but do not modify sp *)
Definition pc_irm sanitized_inst reg : IRM :=
  fun cond i ti sl sr =>
    let a := Z4 * i + Z8 in
    arm_assemble_all_cond ([
      STR   reg SP Z_4;                  (* str reg, [sp, #-4] *)
      MOVW  reg (a & Z0xffff);           (* movw reg, #a[16:0] *)
      MOVT  reg ((a >> Z16) & Z0xffff);  (* movt reg, #a[32:16] *)
      sanitized_inst                     (* sanitized_inst *)
    ]++arm_table_lookup ti sl sr reg++[  (* reg = table[H(reg)] *)
      ALIGN SP;
      STR   reg SP Z_8;                  (* str reg, [sp, #-8] *)
      LDR   reg SP Z_4;                  (* ldr reg, [sp, #-4] *)
      MOVT Z3 Z23;
      MOVW Z3 Z23;
      LDR   PC  SP Z_8                   (* ldr pc, [sp, #-8] *)
    ]) cond.
(* irm for instructions that use pc as a destination register, and do modify sp *)
Definition pc_sp_irm sanitized_inst reg reg2 : IRM :=
  fun cond i ti sl sr =>
    let a := Z4 * i + Z8 in
    arm_assemble_all_cond ([
      STMDB3 SP reg reg2 PC;
      MOV   reg2 SP;
      MOVW  reg (a & Z0xffff);                (* movw reg, #a[16:0] *)
      MOVT  reg ((a >> Z16) & Z0xffff);       (* movt reg, #a[32:16] *)
      sanitized_inst                          (* sanitized_inst *)
    ]++arm_table_lookup ti sl sr reg++[       (* reg = table[H(reg)] *)
      STR   reg reg2 (Z_4);                   (* str reg, [sp, #-8 - stack offset] *)
      LDMDB3 reg2 reg reg2 PC                 (* ldmdb reg2, {reg, reg2, pc} *)
    ]) cond.
Definition rewrite_pc sanitized_inst reg := rewrite_w_table (pc_irm sanitized_inst reg).
Definition rewrite_pc_sp sanitized_inst reg reg2 := rewrite_w_table (pc_sp_irm sanitized_inst reg reg2).
Definition rewrite_pc_no_jump sanitized_inst cond i reg tc : NewInst :=
  let a := Z4 * i + Z8 in
  wo_table (arm_assemble_all_cond ([
    STR reg SP Z_4;                    (* str reg, [sp, #-4] *)
    MOVW reg (a & Z0xffff);            (* movw reg, #a[16:0] *)
    MOVT reg ((a >> Z16) & Z0xffff);   (* movt reg, #a[32:16] *)
    sanitized_inst;                    (* santitized_inst *)
    LDR reg SP Z_4                     (* ldr reg, [sp, #-4] *)
  ]) cond) tc.
Definition rewrite_pc_sp_no_jump sanitized_inst cond i reg reg2 tc : NewInst :=
  let a := Z4 * i + Z8 in
  wo_table (arm_assemble_all_cond ([
    STMDB2 SP reg reg2;                (* stmdb sp, {reg, reg2} *)
    MOV reg2 SP;                       (* mov reg2, sp *)
    MOVW reg (a & Z0xffff);            (* movw reg, #a[16:0] *)
    MOVT reg ((a >> Z16) & Z0xffff);   (* movt reg, #a[32:16] *)
    sanitized_inst;                    (* santitized_inst *)
    LDMDB2 reg2 reg reg2               (* ldmdb reg2, {reg, reg2} *)
  ]) cond) tc.

Definition canonical_z w z := (Z.land (z + (Z1 << (w-Z1))) (Z.ones w)) - (Z1 << (w-Z1)).
Definition rewrite_b_bl (l: bool) (cond imm24: Z) i dis i2i' ai tc : NewInst :=
  let j := Z.land (i + Z2 + (canonical_z Z24 imm24)) (Z.ones Z30) in
  let dst := if (contains j dis) then (i2i' j) else ai in
  match GOTOz l cond (i2i' i) dst with
  | Some z => Some ([z], nil, tc)
  | None => match GOTOz true cond (i2i' i) ai with
            | Some z => Some ([z], nil, tc)
            | None => None
            end
  end.
Definition rewrite_b := rewrite_b_bl false.
Definition rewrite_bl := rewrite_b_bl true.

(* "mov lr, pc" should use new pc, not old pc
it's possible that the old pc should be used, if lr is later used as a data memory address,
but a compiler would most likely pick a general register instead of lr if that was the case

although any rewritten jump would be able to handle the old pc value correctly, this inst
is sometimes used when calling a kernel user helper function, which we cannot rewrite *)
Definition rewrite_mov_lr_pc cond i i2i' tc : NewInst :=
  let pc' := Z4 * i2i' (i+Z2) in
  wo_table (arm_assemble_all_cond ([
    MOVW  LR (pc' & Z0xffff);
    MOVT  LR ((pc' >> Z16) & Z0xffff)
  ]) cond) tc.

Definition _unused_reg (base r0 r1 r2: Z) :=
  if (r0 =? base) || (r1 =? base) || (r2 =? base) then
    if (r0 =? base+Z1) || (r1 =? base+Z1) || (r2 =? base+Z1) then
      if (r0 =? base+Z2) || (r1 =? base+Z2) || (r2 =? base+Z2) then base+Z3
      else base+Z2
    else base+Z1
  else base.
Definition unused_reg := _unused_reg Z0.
Definition unused_reg_high := _unused_reg Z4.
Definition goto_abort i' ai tc : NewInst :=
  match GOTOz true Z14 i' ai with
  | Some z => Some ([z], nil, tc)
  | None => None
  end.
Definition cd cond := if (cond <? Z14) then Z1 else Z0.
Definition rwl_pc c := cd c + Z17.
Definition rwl_pc_sp c := cd c + Z14.
Extraction Inline rwl_pc.
Extraction Inline rwl_pc_sp.
Set Extraction AutoInline.
Definition rewrite_inst_len (i bi: Z) (txt: list Z) (z: Z) : Z :=
  let decoded := arm_decode z in
  match decoded with
  (* branching *)
  | ARM_BX cond reg =>
      if (reg <? 0) || (reg >=? PC) then Z1
      else cd cond + Z8
  | ARM_BLX_r cond reg =>
      if (reg <? 0) || (reg >=? PC) then Z1
      else cd cond + Z8
  | ARM_B cond imm24 => Z1
  | ARM_BL cond imm24 => Z1
  (* data processing *)
  | ARM_data_r op cond s Rn Rd imm5 type Rm =>
      if (Rd =? PC) then rwl_pc cond
      else if (Rn =? PC) || (Rm =? PC) then
        if (match op with ARM_MOV => Rd =? LR | _ => false end) then
          cd cond + Z2
        else if (Rd =? SP) then
          cd cond + Z6
        else
          cd cond + Z5
      else Z1
  | ARM_data_i op cond s Rn Rd imm12 =>
      if (Rd =? PC) then rwl_pc cond
      else if (Rn =? PC) then
        if (Rd =? SP) then
          cd cond + Z6
        else
          cd cond + Z5
      else Z1
  (* load/store *)
  | ARM_ls_i ARM_LDR cond P U W Rn Rt imm12 =>
      if (Rt =? PC) then
        if ((Rn =? SP) && ((P =? Z0) || (W =? Z1))) then rwl_pc_sp cond
        else rwl_pc cond
      else if (Rn =? PC) then
        let loadedi := (Z.land (if (U =? Z1) then i + Z2 + (imm12>>Z2) else i + Z2 - (imm12>>Z2)) (Z.ones Z30)) in
        let listi := loadedi-bi in
        match Z.land Z3 imm12 =? Z0, listi >=? Z0, nth_error txt (Z.to_nat (listi)) with
        | true, true, Some lv => cd cond + Z2
        | _, _, _ =>
            if (Rt =? SP) then cd cond + Z6
            else cd cond + Z5
        end
      else Z1
  | ARM_ls_r ARM_LDR cond P U W Rn Rt imm5 type Rm =>
      if (Rt =? PC) then
        if ((Rn =? SP) && ((P =? Z0) || (W =? Z1))) then rwl_pc_sp cond
        else rwl_pc cond
      else if (Rn =? PC) || (Rm =? PC) then
        if (Rt =? SP) then cd cond + Z6
        else cd cond + Z5
      else Z1
  | ARM_lsm op cond W Rn register_list =>
      if (register_list <? Z0) || (Rn <? Z0) || (Rn >=? Z15) then Z1
      else if (bitb register_list Z15 =? Z0) (* pc is not in reg list *)
      || (match op with | ARM_STMDA | ARM_STMDB | ARM_STMIA | ARM_STMIB => true | _ => false end) then Z1
      else
        cd cond + Z12
  | ARM_vls is_load is_single cond U D Rn Vd imm8 =>
      if (Rn =? PC) then
        cd cond + Z5
      else Z1
  (* | ARM_sync_s ARM_sync_word cond Rn Rd Rt => *)
  (*    match arm_assemble_all_cond [ STR Rt Rn 0; MOVW Rd 0 ] cond with *)
  (*    | Some z => Some (z, nil) *)
  (*    | None => None *)
  (*    end *)
  (* unchanged *)
  | ARM_extra_ls_i op cond P U W Rn Rt imm4H imm4L =>
      if (Rn =? PC) then
        if (match op with ARM_STRH | ARM_STRD => false | _ => Rt =? SP end) then
          cd cond + Z6
        else cd cond + Z5
      else Z1
  | ARM_extra_ls_r op cond P U W Rn Rt Rm =>
      if (Rn =? PC) then
        if (match op with ARM_STRH | ARM_STRD => false | _ => Rt =? SP end) then
          cd cond + Z6
        else cd cond + Z5
      else Z1
  | _ => Z1
  end.
Definition rewrite_inst (tc: TableCache) (i2i': Z -> Z) (z: Z) (dis: list Z) (i ti ai bi: Z) (txt: list Z) : NewInst :=
  let unchanged := Some ([z], nil, tc) in
  let abort := goto_abort (i2i' i) ai tc in
  let decoded := arm_decode z in
  if (negb (contains (i+Z1) dis)) then None else
  match decoded with
  (* branching *)
  | ARM_BX cond reg =>
      if (reg <? 0) || (reg >=? PC) then rewrite_b cond 0 i dis i2i' ai tc (* bx pc is just a static branch in arm mode *)
      else rewrite_bx reg tc dis i2i' cond i ti ai
  | ARM_BLX_r cond reg =>
      if (reg <? 0) || (reg >=? PC) then abort (* this can't happen (unpredictable), but it's easier to just do this than write a proof about the decoder *)
      else rewrite_blx reg tc dis i2i' cond i ti ai
  | ARM_B cond imm24 => rewrite_b cond imm24 i dis i2i' ai tc
  | ARM_BL cond imm24 => rewrite_bl cond imm24 i dis i2i' ai tc
  (* data processing *)
  | ARM_data_r op cond s Rn Rd imm5 type Rm =>
      let reg := unused_reg Rn Rd Rm in
      let reg2 := unused_reg_high Rn Rd Rm in
      let Rn' := if (Rn =? PC) then reg else Rn in
      let Rd' := if (Rd =? PC) then reg else Rd in
      let Rm' := if (Rm =? PC) then reg else Rm in
      let sanitized_inst := ARM_data_r op Z14 s Rn' Rd' imm5 type Rm' in
      if (Rd =? PC) then
        rewrite_pc sanitized_inst reg tc dis i2i' cond i ti ai
      else if (Rn =? PC) || (Rm =? PC) then
        if (match op with ARM_MOV => Rd =? LR | _ => false end) then
          rewrite_mov_lr_pc cond i i2i' tc
        else if (Rd =? SP) then
          rewrite_pc_sp_no_jump sanitized_inst cond i reg reg2 tc
        else
          rewrite_pc_no_jump sanitized_inst cond i reg tc
      else unchanged
  | ARM_data_rsr _ _ _ _ _ _ _ _ => unchanged
  | ARM_data_i op cond s Rn Rd imm12 =>
      let reg := unused_reg Rn Rd Z0 in
      let reg2 := unused_reg_high Rn Rd Z0 in
      let Rn' := if (Rn =? PC) then reg else Rn in
      let Rd' := if (Rd =? PC) then reg else Rd in
      let sanitized_inst := ARM_data_i op Z14 s Rn' Rd' imm12 in
      if (Rd =? PC) then
        rewrite_pc sanitized_inst reg tc dis i2i' cond i ti ai
      else if (Rn =? PC) then
        if (Rd =? SP) then
          rewrite_pc_sp_no_jump sanitized_inst cond i reg reg2 tc
        else
          rewrite_pc_no_jump sanitized_inst cond i reg tc
      else unchanged
  (* load/store *)
  | ARM_ls_i ARM_LDR cond P U W Rn Rt imm12 =>
      let reg := unused_reg Rn Rt Z0 in
      let reg2 := unused_reg_high Rn Rt Z0 in
      let Rn' := if (Rn =? PC) then reg else Rn in
      let Rt' := if (Rt =? PC) then reg else Rt in
      let sanitized_inst := ARM_ls_i ARM_LDR cond P U W Rn' Rt' imm12 in
      if (Rt =? PC) then
        if ((Rn =? SP) && ((P =? Z0) || (W =? Z1))) then rewrite_pc_sp sanitized_inst reg reg2 tc dis i2i' cond i ti ai
        else rewrite_pc sanitized_inst reg tc dis i2i' cond i ti ai
      else if (Rn =? PC) then
        let loadedi := (Z.land (if (U =? Z1) then i + Z2 + (imm12>>Z2) else i + Z2 - (imm12>>Z2)) (Z.ones Z30)) in
        let listi := loadedi-bi in
        match Z.land Z3 imm12 =? Z0, listi >=? Z0, nth_error txt (Z.to_nat (listi)) with
        | true, true, Some lv =>
             wo_table (arm_assemble_all_cond [
               MOVW  Rt (lv & Z0xffff);
               MOVT  Rt ((lv >> Z16) & Z0xffff)
             ] cond) tc
        | _, _, _ =>
            if (Rt =? SP) then rewrite_pc_sp_no_jump sanitized_inst cond i reg reg2 tc
            else rewrite_pc_no_jump sanitized_inst cond i reg tc
        end
      else unchanged
  | ARM_ls_r ARM_LDR cond P U W Rn Rt imm5 type Rm =>
      let reg := unused_reg Rn Rt Rm in
      let reg2 := unused_reg_high Rn Rt Rm in
      let Rn' := if (Rn =? PC) then reg else Rn in
      let Rt' := if (Rt =? PC) then reg else Rt in
      let Rm' := if (Rm =? PC) then reg else Rm in
      let sanitized_inst := ARM_ls_r ARM_LDR cond P U W Rn' Rt' imm5 type Rm' in
      if (Rt =? PC) then
        if ((Rn =? SP) && ((P =? Z0) || (W =? Z1))) then rewrite_pc_sp sanitized_inst reg reg2 tc dis i2i' cond i ti ai
        else rewrite_pc sanitized_inst reg tc dis i2i' cond i ti ai
      else if (Rn =? PC) || (Rm =? PC) then
        if (Rt =? SP) then rewrite_pc_sp_no_jump sanitized_inst cond i reg reg2 tc
        else rewrite_pc_no_jump sanitized_inst cond i reg tc
      else unchanged
  | ARM_lsm op cond W Rn register_list =>
      if (register_list <? Z0) || (Rn <? Z0) || (Rn >=? Z15) then abort (* not possible *)
      else if (bitb register_list Z15 =? Z0) (* pc is not in reg list *)
      || (match op with | ARM_STMDA | ARM_STMDB | ARM_STMIA | ARM_STMIB => true | _ => false end) then unchanged
      else
        let reg := if (Rn =? Z0) then Z1 else Z0 in
        rewrite_ldm_pc op Rn register_list reg decoded tc dis i2i' cond i ti ai
  | ARM_vls is_load is_single cond U D Rn Vd imm8 =>
      if (Rn =? PC) then
        let sanitized_inst := ARM_vls is_load is_single cond U D Z0 Vd imm8 in
        rewrite_pc_no_jump sanitized_inst cond i Z0 tc
      else unchanged
  (* | ARM_sync_s ARM_sync_word cond Rn Rd Rt => *)
  (*    match arm_assemble_all_cond [ STR Rt Rn 0; MOVW Rd 0 ] cond with *)
  (*    | Some z => Some (z, nil) *)
  (*    | None => None *)
  (*    end *)
  (* unchanged *)
  | ARM_extra_ls_i op cond P U W Rn Rt imm4H imm4L =>
      let reg := if (Rt =? Z0) then Z1 else Z0 in
      let reg2 := if (Rt =? Z2) then Z3 else Z2 in
      let sanitized_inst := ARM_extra_ls_i op cond P U W reg Rt imm4H imm4L in
      if (Rn =? PC) then
        if (match op with ARM_STRH | ARM_STRD => false | _ => Rt =? SP end) then
          rewrite_pc_sp_no_jump sanitized_inst cond i reg reg2 tc
        else rewrite_pc_no_jump sanitized_inst cond i reg tc
      else unchanged
  | ARM_extra_ls_r op cond P U W Rn Rt Rm =>
      let reg := if (Rt =? Z0) then Z1 else Z0 in
      let reg2 := if (Rt =? Z2) then Z3 else Z2 in
      let sanitized_inst := ARM_extra_ls_r op cond P U W reg Rt Rm in
      if (Rn =? PC) then
        if (match op with ARM_STRH | ARM_STRD => false | _ => Rt =? SP end) then
          rewrite_pc_sp_no_jump sanitized_inst cond i reg reg2 tc
        else rewrite_pc_no_jump sanitized_inst cond i reg tc
      else unchanged
  | ARM_ls_i _ _ _ _ _ _ _ _
  | ARM_ls_r _ _ _ _ _ _ _ _ _ _
  | ARM_sync_l _ _ _ _
  | ARM_sync_s _ _ _ _ _
  | ARM_hint _ _
  | ARM_sat _ _ _ _ _
  | ARM_mul _ _ _ _ _ _ _
  | ARM_hmul _ _ _ _ _ _ _ _
  | ARM_MOV_WT _ _ _ _ _
  | ARM_CLZ _ _ _
  | ARM_SVC _ _
  | ARM_PLD_r _ _ _ _ _ _
  | ARM_PLD_i _ _ _ _
  | ARM_coproc_m _ _ _ _ _ _ _ _
  | ARM_pas _ _ _ _ _ _ _
  | ARM_rev _ _ _ _
  | ARM_extend _ _ _ _ _ _ _
  | ARM_vlsm _ _ _ _ _ _ _ _ _ _
  | ARM_VMOV_i _ _ _ _ _ _
  | ARM_VMOV_r2 _ _ _ _ _ _ _
  | ARM_VMOV_r1 _ _ _ _ _
  | ARM_VCMP _ _ _ _ _ _ _
  | ARM_VMRS _ _
  | ARM_vfp _ _ _ _ _ _ _ _ _ _
  | ARM_VCVT_ds _ _ _ _ _ _
  | ARM_VCVT_fpi _ _ _ _ _ _ _ _
  | ARM_VCVT_fpf _ _ _ _ _ _ _ _ _
  | ARM_vfp_other _ _ _ _ _ _ _
  | idk
      => unchanged

  | _ => abort
  end.

(*
   pol - maps indexes to lists of valid destination indexes
   i2i' - maps old indexes to new indexes
   zs - list of instruction encodings
   i - current index
   ti - table index
   ai - abort index
   bi - base index
   txt - same as zs but doesn't change when recursing
*)
Fixpoint _rewrite (zs: list Z) (tc: TableCache) (pol: Z -> list Z) (i2i': Z -> Z) (i ti ai bi: Z) (txt: list Z) : option (list (list Z) * list (list Z)) :=
  if (i >=? Z1 << Z30) || (i2i' i >=? Z1 << Z30 - Z1) || (ti >=? Z1 << Z30) then None else
  match zs with
  | z::zs =>
      match rewrite_inst tc i2i' z (pol i) i ti ai bi txt with
      | None => None
      | Some (z', table, tc') =>
          let ti' := ti + Z.of_nat (length table) in
          match _rewrite zs tc' pol i2i' (i+Z1) ti' ai bi txt with
          | None => None
          | Some (z_t, table_t) => Some (z'::z_t, table::table_t)
          end
      end
  | nil =>
      match GOTOz true Z14 (i2i' i) ai with
      | Some z => Some ([[z]], nil)
      | None => None
      end
  end.

Fixpoint _make_i's (z's: list (list Z)) i' :=
  match z's with
  | z'::tail => i'::_make_i's tail (i' + Z.of_nat (length z'))
  | nil => nil
  end.
Definition make_i's lens i' :=
  rev (snd (fold_left (fun a b => let sum := fst a + b in (sum, i' + fst a :: snd a)) lens (0, nil))).
Definition get l n := nth (Z.to_nat n) l 0.
Definition of_list (x: list Z) := x.
Definition make_i2i' bi bi' ai lens :=
  let i's := make_i's lens bi' in
  let ie := bi + Z.of_nat (length lens) in
  let ie' := bi' + (fold_left Z.add lens Z0) in
  let arr := of_list i's in
  fun x => if (x <? bi) || (x >=? ie) then if (x <? bi') || (x >=? ie') then x else ai else get arr (x-bi).

Fixpoint _mapi A B i f (l: list A) : list B :=
  match l with
  | a::t => f i a::_mapi A B (i+1) f t
  | nil => nil
  end.
Definition mapi {A B} := _mapi A B 0.
Extract Inlined Constant mapi => "List.mapi".

Definition cfi_rw (pol: Z -> list Z) (code: list Z) (bi bi' ti ai: Z) :=
  let tc := fun _ => None in
  let irm_lens := mapi (fun i => rewrite_inst_len (bi+i) bi (of_list code)) code in
  let i2i' := make_i2i' bi bi' ai irm_lens in
  _rewrite code tc pol i2i' bi ti ai bi (of_list code).

Extract Inductive Z => int [ "0" "" "(~-)" ].
Extract Inductive nat => int [ "0" "" ].
Extract Inductive bool => "bool" [ "true" "false" ].
Extract Inductive option => "option" [ "Some" "None" ].
Extract Inductive prod => "( * )"  [ "(,)" ].
Extract Inductive list => "list" [ "[]" "(::)" ].
Extract Inlined Constant id => "int".
Extract Inlined Constant Z0xff => "0xff".
Extract Inlined Constant Z4095 => "4095".
Extract Inlined Constant Z1 => "1".
Extract Inlined Constant Z2 => "2".
Extract Inlined Constant Z3 => "3".
Extract Inlined Constant Z4 => "4".
Extract Inlined Constant Z5 => "5".
Extract Inlined Constant Z6 => "6".
Extract Inlined Constant Z7 => "7".
Extract Inlined Constant Z8 => "8".
Extract Inlined Constant Z9 => "9".
Extract Inlined Constant Z10 => "10".
Extract Inlined Constant Z11 => "11".
Extract Inlined Constant Z12 => "12".
Extract Inlined Constant Z13 => "13".
Extract Inlined Constant Z14 => "14".
Extract Inlined Constant Z15 => "15".
Extract Inlined Constant Z16 => "16".
Extract Inlined Constant Z17 => "17".
Extract Inlined Constant Z18 => "18".
Extract Inlined Constant Z19 => "19".
Extract Inlined Constant Z20 => "20".
Extract Inlined Constant Z21 => "21".
Extract Inlined Constant Z22 => "22".
Extract Inlined Constant Z23 => "23".
Extract Inlined Constant Z24 => "24".
Extract Inlined Constant Z25 => "25".
Extract Inlined Constant Z26 => "26".
Extract Inlined Constant Z27 => "27".
Extract Inlined Constant Z28 => "28".
Extract Inlined Constant Z29 => "29".
Extract Inlined Constant Z30 => "30".
Extract Inlined Constant Z31 => "31".
Extract Inlined Constant Z32 => "32".
Extract Inlined Constant Z_32 => "(-32)".
Extract Inlined Constant Z_4 => "(-4)".
Extract Inlined Constant Z_8 => "(-8)".
Extract Inlined Constant Z0xffff => "0xffff".
Extract Inlined Constant Z32767 => "32767".
Extract Inlined Constant Z1245169 => "1245169".
Extract Inlined Constant Z1245171 => "1245171".
Extract Inlined Constant Z33554428 => "33554428".
Extract Inlined Constant Z_33554432 => "(-33554432)".
Extract Inlined Constant Z4294967296 => "4294967296".
Extract Inlined Constant Z_8388608 => "(-8388608)".
Extract Inlined Constant Z8388607 => "8388607".
Extract Inlined Constant Z.opp => "(~-)".
Extract Inlined Constant Z.ltb => "(<)".
(* maybe use library that has popcount instrinsic? *)
Extract Constant Z_popcount => "(fun z -> bitb z 0 + bitb z 1 + bitb z 2 + bitb z 3 + bitb z 4 + bitb z 5 + bitb z 6 + bitb z 7 + bitb z 8 + bitb z 9 + bitb z 10 + bitb z 11 + bitb z 12 + bitb z 13 + bitb z 14 + bitb z 15 + bitb z 16 + bitb z 17 + bitb z 18 + bitb z 19 + bitb z 20 + bitb z 21 + bitb z 22 + bitb z 23 + bitb z 24 + bitb z 25 + bitb z 26 + bitb z 27 + bitb z 28 + bitb z 29 + bitb z 30 + bitb z 31)".
Extract Inlined Constant Z.abs => "(abs)".
Extract Inlined Constant internal_Z_beq => "(=)".
Extract Inlined Constant Z.gtb => "(>)".
Extract Inlined Constant Z.geb => "(>=)".
Extract Inlined Constant Z.leb => "(<=)".
Extract Inlined Constant Z.add => "(+)".
Extract Inlined Constant Z.sub => "(-)".
Extract Inlined Constant Nat.sub => "(-)".
Extract Inlined Constant Z.mul => "( * )".
Extract Inlined Constant Z.modulo => "(fun x y -> ((x mod y) + y) mod y)".
Extract Inlined Constant Z.shiftl => "(lsl)".
Extract Inlined Constant Z.ones => "(fun x -> ((lsl) 1 x)-1)".
Extract Inlined Constant Z.shiftr => "(lsr)".
Extract Inlined Constant Z.land => "(land)".
Extract Inlined Constant Z.lor => "(lor)".
Extract Inlined Constant Z.max => "(max)".
Extract Inlined Constant Z.lxor => "(lxor)".
Extract Inlined Constant Z.eqb => "(=)".
Extract Inlined Constant length => "List.length".
Extract Inlined Constant concat => "List.flatten".
Extract Inlined Constant contains => "(fun _ _ -> true)".
Extract Inlined Constant negb => "(not)".
Extract Inlined Constant nth_error => "(fun a i -> if i < Array.length a then Some (Array.get a i) else None)".
Extract Inlined Constant app => "List.append".
Extract Inlined Constant map => "List.map".
Extract Inlined Constant combine => "List.combine".
Extract Inlined Constant Z.to_nat => "".
Extract Inlined Constant Z.of_nat => "".
Extract Inductive sigT => "( * )"  [ "(,)" ].
Extract Inlined Constant projT1 => "fst".
Extract Inlined Constant projT2 => "snd".
Extract Inlined Constant fst => "fst".
Extract Inlined Constant snd => "snd".
Extract Inlined Constant rev => "List.rev".
Extract Inlined Constant fold_left => "(fun f l a -> List.fold_left f a l)".
Extract Inlined Constant get => "Array.get".
Extract Inlined Constant of_list => "Array.of_list".
Extract Inlined Constant id => "Fun.id".
Separate Extraction cfi_rw.
